/*
 * This software is licensed under the MIT License.
 *
 * Copyright (c) 2019 Geoff M. Granum
 */


plugins {
    id 'java'
    id 'maven-publish'
}

version = rootProject['project.version']
group = 'com.fetherbrik.bootstrap'

dependencies {

    implementation project(":core")

    implementation "commons-io:commons-io:${rootProject['versions.commonsIo']}"
    implementation "org.apache.commons:commons-lang3:${rootProject['versions.commons-lang3']}"
    implementation "org.apache.commons:commons-text:${rootProject['versions.commons-text']}"
    implementation "org.apache.httpcomponents:httpcore:${rootProject['versions.httpcore']}"
    implementation "commons-codec:commons-codec:${rootProject['versions.commonsCodec']}"

    implementation "com.google.guava:guava:${rootProject['versions.guava']}"
    implementation "com.google.code.findbugs:jsr305:${rootProject['versions.findbugs']}"

    implementation "org.apache.logging.log4j:log4j-api:${rootProject['versions.log4j']}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${rootProject['versions.log4j']}"
    runtimeOnly "org.apache.logging.log4j:log4j-jcl:${rootProject['versions.log4j']}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${rootProject['versions.jackson']}"

    implementation "com.google.inject.extensions:guice-servlet:${rootProject['versions.guice']}"
    implementation("org.jboss.resteasy:resteasy-jaxrs:${rootProject['versions.resteasy']}")
    implementation "org.jboss.resteasy:resteasy-jackson2-provider:${rootProject['versions.resteasy']}"
    implementation "org.jboss.resteasy:resteasy-jsapi:${rootProject['versions.resteasy']}"
    implementation("org.jboss.resteasy:resteasy-guice:${rootProject['versions.resteasy']}") {
        exclude module: 'guice'
    }

    implementation("org.jboss.resteasy:resteasy-client:${rootProject['versions.resteasy']}")
    compileOnly "javax.servlet:javax.servlet-api:3.1.0"

    implementation "org.eclipse.jetty:jetty-server:${rootProject['versions.jetty']}"
    implementation "org.eclipse.jetty:jetty-servlet:${rootProject['versions.jetty']}"

    testImplementation "org.testng:testng:${rootProject['versions.testNg']}"
    testImplementation "org.mockito:mockito-core:${rootProject['versions.mockito']}"
    testImplementation "org.hamcrest:hamcrest-all:${rootProject['versions.hamcrest']}"
}

javadoc.options({
    addStringOption('tag', 'todo')
})

jar {
    archiveBaseName = project.name
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}


publishing {
    publications {
        releasePublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId group
            artifactId project.name
            version version
            pom {
                name = "FetherBrik Servlet"
                description = "Utilities for building RESTful and JSON client-server applications."
                url = "git@github.com:ggranum/fetherbrik"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/ggranum/fetherbrik"
                    }
                }
                developers {
                    developer {
                        id = "ggranum"
                        name = "Geoff Granum"
                        email = "maven@geoffgranum.com"
                    }
                }
                scm {
                    connection = "https://github.com/ggranum/fetherbrik.git"
                    developerConnection = "git@github.com:ggranum/fetherbrik.git"
                    url = "https://github.com/ggranum/fetherbrik"
                }
            }
        }
    }
}
