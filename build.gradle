/*
 * This software is licensed under the MIT License.
 *
 * Copyright (c) 2015 Geoff M. Granum
 */


buildscript {
    repositories {
        mavenCentral()
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.1.1"
}

subprojects {

    plugins.withId('java', { _ ->
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    })

    configurations.all({
        exclude(module: 'slf4j-simple')
        resolutionStrategy { ->
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            // force certain versions of dependencies (including transitive)
            //  This appends new forced modules:
            force("com.google.guava:guava:${rootProject['versions.guava']}",
                    "commons-codec:commons-codec:${rootProject['versions.commonsCodec']}",
                    "commons-lang:commons-lang3:${rootProject['versions.commons-lang3']}",
                    "commons-io:commons-io:${rootProject['versions.commonsIo']}",
                    "com.google.code.findbugs:jsr305:${rootProject['versions.findbugs']}",
                    "com.fasterxml.jackson.core:jackson-annotations:${rootProject['versions.jackson']}",
                    "com.fasterxml.jackson.core:jackson-core:${rootProject['versions.jackson']}",
                    "com.fasterxml.jackson.core:jackson-databind:${rootProject['versions.jackson']}",
                    "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${rootProject['versions.jackson']}",
                    "org.apache.commons:commons-lang3:${rootProject['versions.commons-lang3']}",
                    "org.apache.httpcomponents:httpcore:${rootProject['versions.httpcore']}",
                    "org.jboss.logging:jboss-logging:${rootProject['versions.jbossLogging']}",
                    "org.slf4j:slf4j-api:${rootProject['versions.slf4j']}",
            )
            //  This would replace existing forced modules with new ones:
            //forcedModules = ['asm:asm-all:3.3.1']

            // cache dynamic versions for 10 minutes
            cacheDynamicVersionsFor 10 * 60, 'seconds'
            // don't cache changing modules at all
            cacheChangingModulesFor 0, 'seconds'
        }
    })

    repositories {
        mavenCentral()
    }

    configurations {
        deployerJars // maven upload archive tasks
    }

    dependencies {
        deployerJars 'org.apache.maven.wagon:wagon-http:2.2' // maven upload archive tasks
    }
}

subprojects {

    if(project.hasProperty('test')) {
        test { ->
            // enable TestNG support (default is JUnit)
            useTestNG()
            workingDir "${project.rootDir}/"
        }
    }
}


